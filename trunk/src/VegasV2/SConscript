Import('env_vc8')

env = env_vc8.Clone()

env['CPPDEFINES'].remove('_MBCS')
env['CPPDEFINES'] += ['_USRDLL', 'UNICODE', '_UNICODE']
env['CPPPATH'] = ['../sdks/Vegas/fio/libFileIO', '..', '../fscommon', '../dfsc', '../utils']
env['LIBS'].remove('common.lib')
env['LIBS'] += ['rpcrt4.lib', 'wininet.lib', 'common_unicode.lib']
env['LINKFLAGS'] += ['/IGNOREIDL']

target = 'dfscVegasV2Out'
env['PDB'] = target + '.pdb'
bin = env.SharedLibrary(
  target,
  [
    'VegasFSMain.cpp',
    'VegasFS.cpp',
    'VegasFSRender.cpp',
    '../sdks/Vegas/fio/libFileIO/SfMem.cpp',
    '../sdks/Vegas/fio/libFileIO/SfAudioUtil.cpp',
    '../sdks/Vegas/fio/libFileIO/SfReadStreams.cpp',
    '../sdks/Vegas/fio/libFileIO/SfWaveFormat.cpp',
    'VegasFS.def',
    env.RES('VegasFS.rc'),
  ]
)

# Add a post-build step to embed the manifest using mt.exe
# The number at the end of the line indicates the file type (1: EXE; 2:DLL).
if not env['dbg']:
  env.AddPostAction(bin,
    'mt.exe -nologo -manifest ${TARGET}.manifest -outputresource:$TARGET;2')
env.Install('#/src/dist/bin', bin[0])
